plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isShellApp = project.getName() == rootProject.shellApp
def isLaunchApp = project.getName() == rootProject.launchApp

if (isShellApp) {
    println "isShellApp: ${isShellApp}"
}

if (isLaunchApp) {
    println "isLaunchApp: ${isLaunchApp}"
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply from: "${rootDir}/config/buildConfigs.gradle"
apply from: "${rootDir}/config/optionConfigs.gradle"
apply from: "${rootDir}/config/featureConfigs.gradle"
apply from: "${rootDir}/config/versionConfigs.gradle"
apply from: "${rootDir}/config/signingConfigs.gradle"
apply from: "${rootDir}/config/libConfigs.gradle"

android {
    resourcePrefix 'combase_'

    defaultConfig {
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            if (isLaunchApp) {
                shrinkResources feature.isMinifyEnabled
            }
        }

        release {
            if (isLaunchApp) {
                shrinkResources true
            }
        }
    }

    flavorDimensions 'library'
    productFlavors {
        if (isLaunchApp) {
            app {
                dimension 'library'
                applicationId 'com.githubyss.common.base'
                versionCode 1
                versionName "1.0"
                manifestPlaceholders = [APP_NAME: "通用Base库"]
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                srcDirs 'src/main/kotlin'
            }
            res {
                srcDirs 'src/main/res'
                srcDirs 'src/main/res/common'

                srcDirs 'src/main/res/activity'
                srcDirs 'src/main/res/fragment'
                srcDirs 'src/main/res/recycler_view'
            }
            manifest {
                srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs {
                srcDirs 'libs'
            }
        }

        if (isLaunchApp) {
            app {
                java {
                    srcDirs 'src/app/java'
                    srcDirs 'src/app/kotlin'
                }
                res {
                    srcDirs 'src/app/res'
                    srcDirs 'src/app/res/common'

                    srcDirs 'src/app/res/homepage'
                    srcDirs 'src/app/res/binding_inline'
                    srcDirs 'src/app/res/binding_reflect'
                    srcDirs 'src/app/res/compose'
                    srcDirs 'src/app/res/mvvm'
                    srcDirs 'src/app/res/mvi'
                    srcDirs 'src/app/res/lifecycle'
                }
                manifest {
                    srcFile 'src/app/AndroidManifest.xml'
                }
            }
        }

        test {
            java {
                srcDirs 'src/test/java'
                srcDirs 'src/test/kotlin'
            }
        }

        all {
            // set -> println "集合 ${set.name} 的文件是 ${set.java.srcDirs}"
        }
    }

    /** Binding 基类需要 */
    dataBinding {
        enabled feature.isDataBindingEnabled
    }

    /** Compose 基类需要 */
    buildFeatures {
        compose feature.isComposeEnabled
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation dependenciesLib.androidx_appcompat
    implementation dependenciesLib.androidx_activity_compose
    implementation dependenciesLib.androidx_localbroadcastmanager
    implementation dependenciesLib.androidx_recyclerview
    implementation dependenciesLib.androidx_compose_material

    debugImplementation dependenciesLib.squareup_leakcanary

    if (isLaunchApp) {
        appImplementation project(':CommonResSdk_Android')

        appImplementation dependenciesLib.androidx_fragment_ktx
        appImplementation dependenciesLib.androidx_constraintlayout

        appImplementation dependenciesLib.google_flexbox
        appImplementation dependenciesLib.google_gson
        appImplementation dependenciesLib.squareup_retrofit2_converter_moshi
        appImplementation dependenciesLib.bumptech_glide
    }
}
